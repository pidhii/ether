let {*} = @(require 'list')
let cmdarg = @(require 'cmdarg')

let options = [
  {name = "long-arg", val = "long-arg", has_arg = true},
  {name = "long-no-arg", val = "long-no-arg", has_arg = false},
  {name = "a", val = "short-arg", has_arg = true},
  {name = "f", val = "short-no-arg", has_arg = false},
  {name = ["help", "h"], val = "h", has_arg = false},
]

let tests = [
  let cmd = ["first", "--long-arg", "arg", "second", "--long-no-arg", "-a", "1", "-f", "third"]
  let (kw, pos) = cmdarg.get options cmd
  ("test-1", fn -> all? id [
      pos eq ["first", "second", "third"],
      assoc "long-arg" kw eq "arg",
      assoc "long-no-arg" kw eq true,
      assoc "short-arg" kw eq "1",
      assoc "short-no-arg" kw eq true,
  ]),

  let cmd =
    ["first","--long-arg=arg","second","--long-no-arg","-a1","-f","third"]
  let (kw, pos) = cmdarg.get options cmd
  ("test-2", fn -> all? id [
      pos eq ["first", "second", "third"],
      assoc "long-arg" kw eq "arg",
      assoc "long-no-arg" kw eq true,
      assoc "short-arg" kw eq "1",
      assoc "short-no-arg" kw eq true]),

  let cmd = ["--long-arg="]
  let (kw, pos) = cmdarg.get options cmd
  ("test-3", fn -> assoc "long-arg" kw eq ""),

  let cmd = ["--long-arg"]
  ("test-4", fn ->
    try cmdarg.get options cmd; false
    with e -> e eq `missing_argument),

  let cmd = ["-a"]
  ("test-5", fn ->
    try cmdarg.get options cmd; false
    with e -> e eq `missing_argument),

  let cmd = ["--long-no-arg=x"]
  ("test-6", fn ->
    try cmdarg.get options cmd; false
    with e -> e eq `unexpected_argument),

  let cmd = ["-fx"]
  ("test-7", fn ->
    try cmdarg.get options cmd; false
    with e -> e eq `unexpected_argument),

  let cmd = ["-o"]
  ("test-8", fn ->
    try cmdarg.get options cmd; false
    with e -> e eq `undefined_option),

  ("test-9", fn ->
    let cmd1 = ["-h"]
    let kw1 = first $ cmdarg.get options cmd1
    let cmd2 = ["--help"]
    let kw2 = first $ cmdarg.get options cmd2
    assoc "h" kw1 eq true && assoc "h" kw2 eq true),
]

let (nok, nfail) = @(require 'test').run [(nil, tests)]
exit 1 when nfail > 0;

